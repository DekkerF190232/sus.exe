package mogroft/world/WorldManager {

  use sus/System;
  use sus/Math;
  use sus/List;
  use sus/Time;
  use sus/Thread;
  use sus/Mutex;
  
  use mogroft/algebra/Vec3i;

  use mogroft/Noise2DFractal;
  use mogroft/Noise2D;

  use mogroft/world/World;
  use mogroft/world/Block;
  use mogroft/world/Blocks;
  use mogroft/world/Chunk;
  use mogroft/world/ChunkList;
  use mogroft/world/ChunkCoordList;
  
  struct WorldManager (
    PTR[World] world,
    Time tmEnqueue,
    ChunkEventHandler chunkEventHandler,
    ChunkCoordList _queue,
    Mutex _mtxQueue,
    ChunkList _buffer,
    Mutex _mtxBuffer
  );
  
  struct ChunkEventHandler(
    funcptr[(PTR[Chunk] chunk)] onLoaded,
    funcptr[(PTR[ChunkCoord] chunkCoord)] onUnloaded
  );

  func WorldManager new(ChunkEventHandler chunkEventHandler) {
    done WorldManager(
      world = RTP[PTR[World]](0),
      tmEnqueue = Time/zero(),
      chunkEventHandler = chunkEventHandler,
      # generator
      _queue = ChunkCoordList/alloc(), 
      _mtxQueue = Mutex/alloc(),
      _buffer = ChunkList/new(),
      _mtxBuffer = Mutex/alloc()
    );
  }

  func loadExample(PTR[WorldManager] this) {
    PTR[World] world = World/alloc();
    DRF(this).world = world;
  }
  
  func init(PTR[WorldManager] this) {
    Thread genThread = Thread/new(
      run: RTP[funcptr[(ptr param)]](funcref(_runGenerator)),
      arg: RTP[ptr](this)
    );
  }
  
  func tick(PTR[WorldManager] this, real32 dt) {
    _unloadUnused(this: this);
    _enqueueToGenerate(this: this);
    _pollGenerated(this: this);
  }
  
  func _unloadUnused(PTR[WorldManager] this) {
    # generation box center: player chunk coordinates
    int32 x = (cast[int32](DRF(DRF(this).world).player.position.x) + (Chunk/SIZE / 2)) / Chunk/SIZE;
    int32 y = (cast[int32](DRF(DRF(this).world).player.position.y) + (Chunk/SIZE / 2)) / Chunk/SIZE;
    int32 z = (cast[int32](DRF(DRF(this).world).player.position.z) + (Chunk/SIZE / 2)) / Chunk/SIZE;
    
    int32 flatRange = 6;
    int32 yRange = 6;
    
    PTR[List] chunkListList = REF(DRF(DRF(this).world).chunkList.list);
    PTR[ChunkList] chunkList = REF(DRF(DRF(this).world).chunkList);
    
    PTR[Chunk] start = RTP[PTR[Chunk]](List/at(
      this: chunkListList,
      i:    0
    ));
    PTR[Chunk] current = RTP[PTR[Chunk]](List/at(
      this: chunkListList,
      i:    DRF(chunkListList).length
    ));
    funcptr[(PTR[ChunkCoord] chunkCoord)] onUnloadedFunc = DRF(this).chunkEventHandler.onUnloaded;
    while (RTP[int32](current) != RTP[int32](start)) {
      current = current - size[Chunk]();
      int32 dx = Math/absi(x: DRF(current).chunkCoord.x - x);
      int32 dz = Math/absi(x: DRF(current).chunkCoord.z - z);
      int32 dy = Math/absi(x: DRF(current).chunkCoord.y - y);
      
      if ((dx > flatRange) || (dz > flatRange) || (dy > yRange)) {
        onUnloadedFunc(chunkCoord: REF(DRF(current).chunkCoord));
        List/remove(this: chunkListList, index: RTP[PTR[ptr]](current));
        done;
      }
    }
  }
  
  func _pollGenerated(PTR[WorldManager] this) {
    PTR[List] chunkListList = REF(DRF(DRF(this).world).chunkList.list);
    
    # load generated chunks
    
    Mutex/lock(this: REF(DRF(this)._mtxBuffer));
    
    int32 startIndex = DRF(chunkListList).length;
    List/addAll(this: chunkListList, other: REF(DRF(this)._buffer.list));
    Mutex/unlock(this: REF(DRF(this)._mtxBuffer));
    funcptr[(PTR[Chunk] chunk)] onLoadedFunc = DRF(this).chunkEventHandler.onLoaded;
    List/clear(this: REF(DRF(this)._buffer.list));

    # call generation listener
    PTR[Chunk] current = RTP[PTR[Chunk]](List/at(
      this: chunkListList,
      i:    startIndex
    ));
    PTR[Chunk] addEnd = RTP[PTR[Chunk]](List/at(
      this: chunkListList,
      i:    DRF(chunkListList).length
    ));
    while (RTP[int32](current) != RTP[int32](addEnd)) {
      #System/fatal(reason: STR"lol");
      onLoadedFunc(chunk: current);
      current = current + size[Chunk]();
    }
  }
  
  func int32 compare(PTR[ptr] a, PTR[ptr] b) {
    PTR[ChunkCoord] ca = RTP[PTR[ChunkCoord]](a);
    PTR[ChunkCoord] cb = RTP[PTR[ChunkCoord]](b);
    
    PTR[WorldManager] this = REF(DRF(mogroft/Mogroft/_mogroft).worldManager);
    
    int32 x = (cast[int32](DRF(DRF(this).world).player.position.x) + (Chunk/SIZE / 2)) / Chunk/SIZE;
    int32 y = (cast[int32](DRF(DRF(this).world).player.position.y) + (Chunk/SIZE / 2)) / Chunk/SIZE;
    int32 z = (cast[int32](DRF(DRF(this).world).player.position.z) + (Chunk/SIZE / 2)) / Chunk/SIZE;
    
    int32 dxa = Math/absi(x: DRF(ca).x - x);
    int32 dya = Math/absi(x: DRF(ca).y - y);
    int32 dza = Math/absi(x: DRF(ca).z - z);
    int32 dista = (dxa * dxa) +
                  (dya * dya) +
                  (dza * dza);
                  
    int32 dxb = Math/absi(x: DRF(cb).x - x);
    int32 dyb = Math/absi(x: DRF(cb).y - y);
    int32 dzb = Math/absi(x: DRF(cb).z - z);
    int32 distb = (dxb * dxb) +
                  (dyb * dyb) +
                  (dzb * dzb);
    
    if (dista < distb) done -1;
    if (dista > distb) done 1;
    
    done 0;
  }
  
  func _runGenerator(PTR[WorldManager] this) {
    PTR[ChunkCoordList] queue = REF(DRF(this)._queue);
    
    ChunkCoordList localQueue = ChunkCoordList/alloc();
    ChunkList localChunks = ChunkList/new();
    
    while (yes) {
      # transfer over chunk coord queue to this thread
      Mutex/lock(this: REF(DRF(this)._mtxQueue));
      
      if (DRF(queue).list.length > 0) {
        List/clear(this: REF(localQueue.list));
        List/addAll(this: REF(localQueue.list), other: REF(DRF(queue).list));
        List/clear(this: REF(DRF(queue).list));
      }
      
      Mutex/unlock(this: REF(DRF(this)._mtxQueue));
      
      List/sort(this: REF(localQueue.list), compare: funcref(compare));
      
      ###
      
      #if (localQueue.list.length > 0) {      
      #  print(chunkCoordList: REF(localQueue));
      #  System/fatal(reason: STR"exit after transfer");
      #}
      
      # generate chunks
      int32 realGenerated = 0;
      int32 i = 0;
      while ((i < localQueue.list.length) && (realGenerated < 10)) {
        PTR[ChunkCoord] chunkCoord = RTP[PTR[ChunkCoord]](List/at(this: REF(localQueue.list), i: i));
        PTR[Chunk] newChunk = ChunkList/add(this: REF(localChunks));
        DRF(newChunk).chunkCoord = DRF(chunkCoord);
        generateChunk(chunk: newChunk);
        if (DRF(newChunk).isAir == no) {
          #System/print(s: STR"generated real: ");
          #print(chunkCoord: REF(DRF(newChunk).chunkCoord));
          #System/println();
          realGenerated = realGenerated + 1;
        }
        i = i + 1;
      }
      #System/println(s: STR"generated chunks");
      ###
      
      # transfer generated chunks over to buffer
      Mutex/lock(this: REF(DRF(this)._mtxBuffer));
      
      List/addAll(this: REF(DRF(this)._buffer.list), other: REF(localChunks.list));
      List/clear(this: REF(localChunks.list));
      
      Mutex/unlock(this: REF(DRF(this)._mtxBuffer));
      ###

      Thread/sleep(ms: 10);
    }
  }
  
  func print(PTR[ChunkCoordList] chunkCoordList) {
    PTR[ChunkCoord] current = RTP[PTR[ChunkCoord]](List/at(
      this: REF(DRF(chunkCoordList).list),
      i: 0)
    );
    PTR[ChunkCoord] end = RTP[PTR[ChunkCoord]](List/at(
      this: REF(DRF(chunkCoordList).list),
      i: DRF(chunkCoordList).list.length)
    );
    System/println(s: STR"================================================================================");
    while (RTP[int32](current) != RTP[int32](end)) {
      print(chunkCoord: current);
      System/println();
      current = current + DRF(chunkCoordList).list._elementSize;
    } 
    System/println(s: STR"---------------------------------------------------------------------------------");
  }
  
  func print(PTR[ChunkCoord] chunkCoord) {
    System/print(i: DRF(chunkCoord).x);
    System/print(s: STR" ");
    System/print(i: DRF(chunkCoord).y);
    System/print(s: STR" ");
    System/print(i: DRF(chunkCoord).z);
  }
  
  static int32 ENQUEUE_INTERVAL_MS = 100;
  
  func _enqueueToGenerate(PTR[WorldManager] this) {
    Time now = Time/now();
    Time delta = Time/sub(a: now, b: DRF(this).tmEnqueue);
    if (Time/lesser(t: delta, ms: ENQUEUE_INTERVAL_MS)) 
      done;
    DRF(this).tmEnqueue = now;
  
    # generation box center: player chunk coordinates
    int32 x = (cast[int32](DRF(DRF(this).world).player.position.x) + (Chunk/SIZE / 2)) / Chunk/SIZE;
    int32 y = (cast[int32](DRF(DRF(this).world).player.position.y) + (Chunk/SIZE / 2)) / Chunk/SIZE;
    int32 z = (cast[int32](DRF(DRF(this).world).player.position.z) + (Chunk/SIZE / 2)) / Chunk/SIZE;
    
    int32 flatRange = 4;
    int32 yRange = 3;
    
    PTR[ChunkList] chunkList = REF(DRF(DRF(this).world).chunkList);
    PTR[ChunkCoordList] queue = REF(DRF(this)._queue);
    
    Mutex/lock(this: REF(DRF(this)._mtxQueue));
  
    int32 cy = y - yRange;
    while (cy < (y + yRange)) {
      int32 cz = z - flatRange;
      while (cz < (z + flatRange)) {
        int32 cx = x - flatRange;
        while (cx < (x + flatRange)) {
          {
            ChunkCoord chunkCoord = ChunkCoord(x = cx, y = cy, z = cz);
          
            PTR[Chunk] loaded = ChunkList/find(
              this: chunkList,
              chunkCoord: chunkCoord
            );
            if (RTP[int32](loaded) != 0) break;
            
            PTR[ChunkCoord] enqueued = ChunkCoordList/find(
              this: queue,
              element: chunkCoord
            );
            if (RTP[int32](enqueued) != 0) break;
            
            DRF(ChunkCoordList/add(this: queue)) = chunkCoord;
            
            #System/print(s: STR"enqueued: ");
            #print(chunkCoord: REF(chunkCoord));
            #System/println();
          }
          cx = cx + 1;
        }
        cz = cz + 1;
      }
      cy = cy + 1;
    }
    
    Mutex/unlock(this: REF(DRF(this)._mtxQueue));
  }
  
  func int32 hash(int32 x) {  
    int8 w = cast[int8](8 * size[int32]());
    int8 m = cast[int8](4 * size[int32]());
    int8 wmm = cast[int8](cast[int32](w) - cast[int32](m));
    int32 k = @9332801;
    k = k ^ (k >> wmm); 
    done (x * k) >> (wmm);
  }
  
  func generateTree(PTR[Chunk] chunk, int32 x, int32 y, int32 z) {
    #int32 totalHeight = (31* (31 * hash(x: ) 
  }
  
  func boo willBeAir(PTR[ChunkCoord] chunkCoord) {
    done DRF(chunkCoord).y >= 0;
  }
  
  func generateChunk(PTR[Chunk] chunk) {
    int32 seed = 1;
  
    #System/print(s: STR"generating: ");
    #print(chunkCoord: REF(DRF(chunk).chunkCoord));
    #System/println();
    
    #Time timePerlin = Time/zero();
    #int32 countPerlin = 0;
    
    DRF(chunk).isAir = willBeAir(chunkCoord: REF(DRF(chunk).chunkCoord));
    if (DRF(chunk).isAir) done;
    
    PTR[Block] blocks = RTP[PTR[Block]](REF(DRF(chunk).blocks));
    
    int32 z = 0;
    while (z < Chunk/SIZE) {
      int32 x = 0;
      while (x < Chunk/SIZE) {
        int32 bx = DRF(chunk).chunkCoord.x * Chunk/SIZE + x;
        
        real32 stoneHeight = 50.0;
        real32 dirtHeight = 5.0;
        real32 totalHeight = stoneHeight + dirtHeight;
        
        int32 bz = DRF(chunk).chunkCoord.z * Chunk/SIZE + z;
              
        int32 stoney = cast[int32]((0.0 - totalHeight) + (Noise2DFractal/noise(
          x: cast[real32](bx) * 0.07,
          y: cast[real32](bz) * 0.07,
          seed: seed,
          levels: 2
        ) * stoneHeight));
        
        int32 dirty = stoney + cast[int32]((Noise2D/perlin(
          x: cast[real32](bx) * 0.07,
          y: cast[real32](bz) * 0.07,
          seed: seed
        ) * dirtHeight));
      
        int32 y = 0;
        while (y < Chunk/SIZE) {
          PTR[Block] block = blocks + (z * Chunk/SIZE * Chunk/SIZE) + (y * Chunk/SIZE) + x;
          int32 by = DRF(chunk).chunkCoord.y * Chunk/SIZE + y;
          
          if (by >= 0) {
            # generate above zero
          
            DRF(block) = Blocks/BLOCK_AIR;
            x = x + 1;
            break 2;
          }
          
          # generate below zero
          
          #Time t1 = Time/now();
          
          #countPerlin = countPerlin + 1;
          #Time t2 = Time/now();
          #Time diff = Time/sub(a: t2, b: t1);
          #timePerlin = Time/add(a: timePerlin, b: diff);
          
          if (by < stoney) {
            DRF(block) = Blocks/BLOCK_STONE;
          } else if (by == dirty) {
            DRF(block) = Blocks/BLOCK_GRASS;
          } else if (by < dirty) {
            DRF(block) = Blocks/BLOCK_DIRT;
          } else {
            DRF(block) = Blocks/BLOCK_AIR;
          }
        
          y = y + 1;
        }
        
        x = x + 1;
      }
      z = z + 1;
    }
    
    #System/print(s: STR"  perlin count: ");
    #System/println(i: countPerlin);
    #System/print(s: STR"  perlin duration: ");
    #System/print(r: cast[real32](Time/mu(t: timePerlin)) / 1000000.0, digits: 6);
    #System/println();
    #timePerlin = Time/zero();
  }

}

#package mogroft/world/BatchGenerator {
#  
#  # [ ] 1. input coordinate list to generate
#  # [ ] 2. chunk buffers where they are generated
#  # [ ] 3. nevermind i will never do this lol, too much!
#  
#  struct BatchGenerator(
#    Mutex 
#  );
#  
#  func create() {
#    
#  }  
#  
#}