
package mogroft/Window {
  
  use sus/native/Win;
  use sus/System;
  use sus/Gl;
  
  static int32 TIMER_ID_WINDOW = 0;
  static int32 INTERVAL_MS = 10; # 100 fps
  
  struct WindowHandler (
    funcptr[()] render,
    funcptr[()] init,
    funcptr[(int32 width, int32 height)] resize,
    funcptr[(int32 vk)] onKeyDown,
    funcptr[(int32 vk)] onKeyUp,
    funcptr[(int32 x, int32 y)] onMouseMove,
    funcptr[(int32 dx, int32 dy)] onMouseMoveDelta,
    funcptr[()] onLooseFocus
  );

  struct WindowState (
    WindowHandle windowHandle,
    DeviceContextHandle deviceContextHandle,
    GlRenderContextHandle glRenderContextHandle,
    WindowHandler windowHandler,
    boo cursorGrabbed
  );

  static PTR[WindowState] _window = RTP[PTR[WindowState]](0);
  static boo initialized = no;

  func run(WindowHandler windowHandler) {
    int32 _2 = Win/SetThreadDpiAwarenessContext(dpiAwarenessContext: Win/DPI_AWARENESS_CONTEXT_PER_MONITOR_AWARE_V2);
  
    WindowState windowState = WindowState(
      windowHandle=RTP[WindowHandle](0),
      deviceContextHandle=RTP[DeviceContextHandle](0),
      glRenderContextHandle=RTP[GlRenderContextHandle](0),
      windowHandler = windowHandler,
      cursorGrabbed = no
    );
    _window = REF(windowState);
    
    # make window
    registerClass();
    createWindow(this: REF(windowState));
    
    # show window
    boo _3 = Win/ShowWindow(handle: windowState.windowHandle, showCommand: Win/SW_SHOWNORMAL);
    boo _4 = Win/UpdateWindow(handle: windowState.windowHandle);

    # create opengl context
    createContext(this: REF(windowState));
    
    # add raw mouse input listener
    RawInputDevice device = RawInputDevice(
      usagePage = Win/HID_USAGE_PAGE_GENERIC,
      usage = Win/HID_USAGE_GENERIC_MOUSE,
      flags = 0,
      targetHandle = windowState.windowHandle
    );
    boo _5 = Win/RegisterRawInputDevices(devices: REF(device), length: 1, rawInputDeviceSize: size[RawInputDevice]());
    
    # use context
    boo _6 = Win/wglMakeCurrent(
      dch: DRF(_window).deviceContextHandle,
      glRenderContextHandle: DRF(_window).glRenderContextHandle
    );
    if (_6 == no) {
      int32 err = Win/GetLastError();
      errMsg(code: err, message: STR"Error setting window context:");
      System/exit(code: err);
    }

    funcptr[()] initFunc = DRF(_window).windowHandler.init;
    initFunc();
    initialized = yes;

    startRepaintTimer(this: REF(windowState));
     
    runMessageLoop(this: REF(windowState));
  }
  
  func startRepaintTimer(PTR[WindowState] this) {
    int32 _1 = Win/SetTimer(
      handle: DRF(this).windowHandle,
      timerId: TIMER_ID_WINDOW,
      intervalMs: INTERVAL_MS,
      timerFunc: RTP[funcptr[CON(stdcall) (WindowHandle handle, int32 _unnamed1, int32 _unnamed2, int32 _unnamed3)]](0)
    );
  }
  
  func createContext(PTR[WindowState] this) {
    DRF(this).deviceContextHandle = Win/GetDC(handle: DRF(this).windowHandle);
    
    if (RTP[int32](DRF(this).deviceContextHandle) == 0) {
      int32 err = Win/GetLastError();
      errMsg(code: err, message: STR"Error loading icon:");
      System/exit(code: err);
    }

    # chose pixel format
    PixelFormatDescriptor pfd = Win/new_PixelFormatDescriptor();
    pfd.size        = cast[int16](size[PixelFormatDescriptor]());
    pfd.version     = cast[int16](1);
    pfd.flags       = Win/PFD_DRAW_TO_WINDOW | Win/PFD_SUPPORT_OPENGL;
    pfd.pixelType   = Win/fc_PFD_TYPE_RGBA();
    pfd.colorBits   = cast[int8](32);
    pfd.alphaBits   = cast[int8](32);
    pfd.depthBits   = cast[int8](24);
    int32 pixelFormat = Win/ChoosePixelFormat(
      dch: DRF(this).deviceContextHandle,
      pfd: REF(pfd)
    );
    if (pixelFormat == 0) {
      int32 err = Win/GetLastError();
      errMsg(code: err, message: STR"Error choosing pixel format:");
      System/exit(code: err);
    }
    
    # set pixel format
    boo _1 = Win/SetPixelFormat(dch: DRF(this).deviceContextHandle, format: pixelFormat, pfd: REF(pfd));
    if (_1 == no) {
      int32 err = Win/GetLastError();
      errMsg(code: err, message: STR"Error setting pixel format:");
      System/exit(code: err);
    }
    
    # create context
    DRF(this).glRenderContextHandle = Win/wglCreateContext(dch: DRF(this).deviceContextHandle);
    if (RTP[int32](DRF(this).glRenderContextHandle) == 0) {
      int32 err = Win/GetLastError();
      errMsg(code: err, message: STR"Error setting pixel format:");
      System/exit(code: err);
    }
  }

  func runMessageLoop(PTR[WindowState] this) {
    WindowMessage message = Win/new_WindowMessage();
    
    while (Win/GetMessageA(
      message:   REF(message),
      handle:    DRF(this).windowHandle,
      filterMin: 0,
      filterMax: 0
    )) {
      boo   _1 = Win/TranslateMessage(message: REF(message));
      int32 _2 = Win/DispatchMessageA(message: REF(message));
    }
  }

  func CON(stdcall) int32 windowProc(WindowHandle handle, int32 message, int32 wParam, int32 lParam) {
    if (message == Win/WM_CREATE) {
      #int32 error = -1;
      #while (error != 0) error = Gl/glGetError();

    } else if (message == Win/WM_PAINT) {
      if (initialized == no) done 0;

      PaintStruct ps = Win/new_PaintStruct();
      
      funcptr[()] renderFunc = DRF(_window).windowHandler.render;
      renderFunc();
      
      DeviceContextHandle _1 = Win/BeginPaint(handle: handle, paintStruct: REF(ps));
      DeviceContextHandle _2 = Win/EndPaint(handle: handle, paintStruct: REF(ps));
      
      done 0;
    } else if (message == Win/WM_SIZE) {

      int32 width = lParam & @ffff;
      int32 height = lParam >> cast[int8](16) & @ffff;
      
      funcptr[(int32 width, int32 height)] resizeFunc = DRF(_window).windowHandler.resize;
      resizeFunc(width: width, height: height);
      
      if (DRF(_window).cursorGrabbed) grabCursor();
      
      done 0;
    } else if (message == Win/WM_TIMER && (wParam == TIMER_ID_WINDOW)) {
      boo _1 = Win/InvalidateRect(handle: handle, rectangle: RTP[PTR[Rectangle]](0), erase: yes);
    } else if (message == Win/WM_KEYDOWN) {
      int32 vk = wParam;
      
      funcptr[(int32 vk)] onKeyDownFunc = DRF(_window).windowHandler.onKeyDown;
      onKeyDownFunc(vk: vk);
    } else if (message == Win/WM_KEYUP) {
      int32 vk = wParam;
      
      funcptr[(int32 vk)] onKeyUpFunc = DRF(_window).windowHandler.onKeyUp;
      onKeyUpFunc(vk: vk);
    } else if (message == Win/WM_KILLFOCUS) {
      funcptr[()] onLooseFocusFunc = DRF(_window).windowHandler.onLooseFocus;
      onLooseFocusFunc();
    } else if (message == Win/WM_LBUTTONDOWN) {
      funcptr[(int32 vk)] onKeyDownFunc = DRF(_window).windowHandler.onKeyDown;
      onKeyDownFunc(vk: Win/VK_LBUTTON);
    } else if (message == Win/WM_LBUTTONUP) {
      funcptr[(int32 vk)] onKeyDownFunc = DRF(_window).windowHandler.onKeyUp;
      onKeyDownFunc(vk: Win/VK_LBUTTON);
    } else if (message == Win/WM_RBUTTONDOWN) {
      funcptr[(int32 vk)] onKeyDownFunc = DRF(_window).windowHandler.onKeyDown;
      onKeyDownFunc(vk: Win/VK_RBUTTON);
    } else if (message == Win/WM_RBUTTONUP) {
      funcptr[(int32 vk)] onKeyDownFunc = DRF(_window).windowHandler.onKeyUp;
      onKeyDownFunc(vk: Win/VK_RBUTTON);
    } else if (message == Win/WM_CLOSE) {
      ungrabCursor();
    } else if (message == Win/WM_MOUSEMOVE) {
      int32 x = lParam & @ffff;
      int32 y = lParam >> cast[int8](16) & @ffff;
      funcptr[(int32 x, int32 y)] onMouseMoveFunc = DRF(_window).windowHandler.onMouseMove;
      onMouseMoveFunc(x: x, y: y);
    } else if (message == Win/WM_INPUT) {
      RawInputHandle rawInputHandle = RTP[RawInputHandle](lParam);
      
      int32 dataSize = -1;
      int32 r = Win/GetRawInputData(
        handle:     rawInputHandle,
        uiCommand:  Win/RID_INPUT,
        data:       RTP[ptr](0),
        dataSize:   REF(dataSize),
        headerSize: size[RawInputHeader]()
      );
      
      if (dataSize <= 0) {
        System/fatal(reason: STR"Failed to read raw input: dataSize <= 0");
      }
      
      PTR[int8] buffer = ARR[int8](dataSize);
      r = Win/GetRawInputData(
        handle:     rawInputHandle,
        uiCommand:  Win/RID_INPUT,
        data:       RTP[ptr](buffer),
        dataSize:   REF(dataSize),
        headerSize: size[RawInputHeader]()
      );
      if (r != dataSize) {
        System/fatal(reason: STR"Failed to read raw input: r != dataSize");
      }
      
      PTR[RawInput] rawInput = RTP[PTR[RawInput]](buffer);
      if (DRF(rawInput).header.type == Win/RIM_TYPEMOUSE) {
        PTR[RawInputDataMouse] data = RTP[PTR[RawInputDataMouse]](REF(DRF(rawInput).data));
        int32 dx = DRF(data).lastX;
        int32 dy = DRF(data).lastY;
        
        funcptr[(int32 dx, int32 dy)] onMouseMoveDeltaFunc = DRF(_window).windowHandler.onMouseMoveDelta;
        onMouseMoveDeltaFunc(dx: dx, dy: dy);
        
        #System/print(s: STR"delta mouse: ");
        #System/print(i: dx);
        #System/print(s: STR",");
        #System/print(i: dy);
        #System/println();
      }
      
    }
  
    done Win/DefWindowProcA(handle: handle, message:message, wParam: wParam, lParam: lParam);
  }
  
  # confines to window size, not point.
  #func grabCursor() {
  #  Rectangle windowSize = Rectangle(left = 0, top = 0, right = 0, bottom = 0);
  #  boo _1 = Win/GetClientRect(
  #    handle:     DRF(_window).windowHandle,
  #    rectangle:  REF(windowSize)
  #  );
  #  boo _2 = Win/ClientToScreen(
  #    handle: DRF(_window).windowHandle,
  #    point:  RTP[PTR[Point]](REF(windowSize))
  #  );
  #  boo _3 = Win/ClientToScreen(
  #    handle: DRF(_window).windowHandle,
  #    point:  RTP[PTR[Point]](REF(windowSize)) + size[Point]()
  #  );
  #  DRF(_window).centerX = windowSize.left + (windowSize.right - windowSize.left) / 2;
  #  DRF(_window).centerY = windowSize.top + (windowSize.bottom - windowSize.top) / 2;
  #  boo _4 = Win/ClipCursor(rectangle: REF(rect));
  #}
  
  # confines to center of window
  func grabCursor() {
    Rectangle windowSize = Rectangle(left = 0, top = 0, right = 0, bottom = 0);
    boo _1 = Win/GetClientRect(
      handle:     DRF(_window).windowHandle,
      rectangle:  REF(windowSize)
    );
    Point center = Point(
      x = windowSize.right / 2,
      y = windowSize.bottom / 2
    );
    boo _2 = Win/ClientToScreen(
      handle: DRF(_window).windowHandle,
      point:  REF(center)
    );
    Rectangle rect = Rectangle(
      left = center.x,
      top = center.y,
      right = center.x + 1,
      bottom = center.y + 1);
    boo _4 = Win/ClipCursor(rectangle: REF(rect));
  }
  
  func ungrabCursor() {
    boo _1 = Win/ClipCursor(rectangle: RTP[PTR[Rectangle]](0));
  }
  
  func setCursorGrabbed(boo cursorGrabbed) {
    boo before = DRF(_window).cursorGrabbed;
    
    DRF(_window).cursorGrabbed = cursorGrabbed;
    
    if (before != cursorGrabbed) {
      if (cursorGrabbed) grabCursor();
      else ungrabCursor();
    }
  }
  
  func setCursorVisible(boo cursorVisible) {
    int32 _ = Win/ShowCursor(show: cursorVisible);
  }
  
  func registerClass() {
    int32 err = 0;

    Win/SetLastError(code:0);

    InstanceHandle instance = RTP[InstanceHandle](0);

    # load icon
    IconHandle icon = Win/LoadIconA(instance: instance, iconName: Win/fc_IDI_WINLOGO());
    err = Win/GetLastError();
    if (err != 0) {
      errMsg(code: err, message: STR"Error loading icon: ");
      System/exit(code: err);
    }

    CursorHandle cursor = Win/LoadCursorA(instance: instance, cursorName: Win/fc_IDC_ARROW());
    err = Win/GetLastError();
    if (err != 0) {
      errMsg(code: err, message: STR"Error loading icon: ");
      System/exit(code: err);
    }

    WindowClassExtendedAscii windowClass = WindowClassExtendedAscii(
      cbSize     = size[WindowClassExtendedAscii](),
      style      = Win/CS_OWNDC | Win/CS_HREDRAW | Win/CS_VREDRAW | Win/CS_DBLCLKS,
      windowProc = funcref(windowProc),
      cbClsExt = 0,
      cbWndExt = 0,
      instance   = instance,
      icon       = icon,
      cursor     = cursor,
      background = RTP[BrushHandle](0),
      menuName   = RTP[PTR[int8]](0),
      className  = STR"Mogroft",
      iconSmall  = RTP[IconHandle](0)
    );

    int32 _ignored = cast[int32](Win/RegisterClassExA(windowClass: REF(windowClass)));
    err = Win/GetLastError();
    if (err != 0) {
      errMsg(code: err, message: STR"Error registering class: ");
      System/exit(code: err);
    }
  }

  func createWindow(PTR[WindowState] this) {
    int32 style = Win/fc_WS_OVERLAPPEDWINDOW()|Win/WS_CLIPSIBLINGS|Win/WS_CLIPCHILDREN;
    
    Rectangle rectangle = Rectangle(
      left = 100,
      top = 100,
      right = 600,
      bottom = 600
    );

    boo res = Win/AdjustWindowRect(
      rectangle: REF(rectangle),
      style:     style, menu: no
    );

    DRF(this).windowHandle = Win/CreateWindowExA(
      extendedStyle: 0,
      className:     STR"Mogroft",
      windowName:    STR"Mogroft [sus.exe] - INC",
      style:         style,
      x:             20,
      y:             20,
      width:         1300,
      height:        600,
      parentHandle:  RTP[WindowHandle](0),
      menu:          RTP[MenuHandle](0),
      instance:      RTP[InstanceHandle](0),
      param:         RTP[ptr](0)
    );

    if (RTP[int32](DRF(this).windowHandle) == 0) {
      int32 code = Win/GetLastError();
      errMsg(code: code, message: STR"Failed to create window. GetLastError: ");
      System/exit(code: code);
    }

  }

  func errMsg(int32 code, PTR[int8] message) {
    System/print(s: message);
    System/print(s: STR" ");

    System/print(i: code);
    System/print(s: STR" = 0x");

    System/println(i_0x: code);
  }

}