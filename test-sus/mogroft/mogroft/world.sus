
package mogroft/world/World {

  use mogroft/world/ChunkList;
  use mogroft/world/Player;
  use mogroft/world/Player;
  use mogroft/algebra/Vec3;
  use sus/Memory;

  struct World (
    Player player,
    ChunkList chunkList
  );
  
  func PTR[World] alloc() {
    PTR[World] this = RTP[PTR[World]](Memory/malloc(bytes: size[World]()));
    DRF(this).player = Player/new();
    DRF(this).chunkList = ChunkList/new();

    done this;
  }
  
}

package mogroft/world/Block {
  
  struct BlockCoord(int32 x, int32 y, int32 z);

  struct Block (
    int8 id
  );
  
  func Block new(int8 id) {
    done Block(id = id);
  }
  
  func boo isSolid(PTR[Block] block) {
    done cast[int32](DRF(block).id) > 1;
  }

}

package mogroft/world/Blocks {
  
  use mogroft/world/Block;
  
  static Block BLOCK_AIR   = Block(id=cast[int8](1));
  static Block BLOCK_STONE = Block(id=cast[int8](2));
  static Block BLOCK_GRASS = Block(id=cast[int8](3));
  static Block BLOCK_DIRT  = Block(id=cast[int8](4));
  static Block BLOCK_LOG   = Block(id=cast[int8](5));
  static Block BLOCK_WOOD  = Block(id=cast[int8](6));
  
  static int32 BLOCK_COUNT = 6;

  func int32 ordinal(Block block) {
    done cast[int32](block.id) - 1;
  }

}

package mogroft/world/Chunk {
  
  use mogroft/world/Block;
  use mogroft/world/Chunk;
  use sus/Memory;

  static int32 SIZE = 16;
  static Chunk CHUNK_EMPTY = Chunk(
    chunkCoord = ChunkCoord(x=0, y=0, z=0),
    blocks = array[Block, 4096](Block(id = cast[int8](0)))
  );

  struct ChunkCoord (
    int32 x,
    int32 y,
    int32 z
  ); # [chunks]

  struct Chunk (
    ChunkCoord chunkCoord,
    array[Block, 4096] blocks # 16^3 = 4096
  );

  func PTR[Chunk] alloc() {
    done RTP[PTR[Chunk]](Memory/malloc(bytes: size[Chunk]()));
  }

  func free(PTR[Chunk] this) {
    Memory/free(pointer: RTP[ptr](this));
  }

}

package mogroft/world/ChunkList {

  use sus/List;
  use mogroft/world/Chunk;

  struct ChunkList(
    List list # <Chunk>
  );

  func ChunkList new() {
    done ChunkList(
      list = List/alloc(elementSize: size[Chunk]())
    );
  }
  
  func PTR[Chunk] add(PTR[ChunkList] this) {
    done RTP[PTR[Chunk]](List/add(this: REF(DRF(this).list), element: RTP[ptr](0)));
  }

  func PTR[Chunk] find(PTR[ChunkList] this, ChunkCoord chunkCoord) {
    PTR[Chunk] current = RTP[PTR[Chunk]](List/at(
      this: REF(DRF(this).list),
      i: 0
    ));
    PTR[Chunk] end = RTP[PTR[Chunk]](List/at(
      this: REF(DRF(this).list),
      i: DRF(this).list.length
    ));

    while (RTP[int32](current) != RTP[int32](end)) {
      if (
        (DRF(current).chunkCoord.x == chunkCoord.x) &&
        (DRF(current).chunkCoord.y == chunkCoord.y) &&
        (DRF(current).chunkCoord.z == chunkCoord.z)
      ) done RTP[PTR[Chunk]](current);
      current = current + size[Chunk]();
    }
    done RTP[PTR[Chunk]](0);
  }

}

package mogroft/world/ChunkCoordList {

  use sus/List;
  use mogroft/world/Chunk;

  struct ChunkCoordList (
    List list
  );

  func ChunkCoordList alloc() {
    done ChunkCoordList(
      list = List/alloc(elementSize: size[ChunkCoord]())
    );
  }
  
  func PTR[ChunkCoord] add(PTR[ChunkCoordList] this) {
    done RTP[PTR[ChunkCoord]](List/add(this: REF(DRF(this).list), element: RTP[ptr](0)));
  }

  func remove(PTR[ChunkCoordList] this, int32 i) {
    List/remove(this: REF(DRF(this).list), i: i);
  }
  
  func remove(PTR[ChunkCoordList] this, PTR[ChunkCoord] index) {
    List/remove(this: REF(DRF(this).list), index: RTP[PTR[ptr]](index));
  }

  func PTR[ChunkCoord] find(PTR[ChunkCoordList] this, ChunkCoord element) {
    PTR[ChunkCoord] current = RTP[PTR[ChunkCoord]](List/at(
      this: REF(DRF(this).list),
      i: 0
    ));
    PTR[ChunkCoord] end = RTP[PTR[ChunkCoord]](List/at(
      this: REF(DRF(this).list),
      i: DRF(this).list.length
    ));

    while (RTP[int32](current) != RTP[int32](end)) {
      if (
        (DRF(current).x == element.x) &&
        (DRF(current).y == element.y) &&
        (DRF(current).z == element.z)
      ) done RTP[PTR[ChunkCoord]](current);
      current = current + size[ChunkCoord]();
    }
    done RTP[PTR[ChunkCoord]](0);
  }

}

package mogroft/world/Player {
  
  use mogroft/algebra/Vec3;
  use sus/Math;

  struct Player (
    Vec3 position, # [blocks]
    real32 yaw, # [deg]
    real32 pitch # [deg]
  );
  
  func lookDelta(PTR[Player] this, real32 dyaw, real32 dpitch) {
    DRF(this).yaw = Math/remainderf(x: DRF(this).yaw + dyaw, y: 360.0);
    
    DRF(this).pitch = DRF(this).pitch + dpitch;
    if (DRF(this).pitch > 90.0) DRF(this).pitch = 90.0;
    if (DRF(this).pitch < -90.0) DRF(this).pitch = -90.0;
  }
  
  func Player new() {
    done Player(
      position = Vec3/new(),
      yaw = 0.0,
      pitch = 0.0
    );
  }
}